

options {
  LOOKAHEAD = 2;
}

PARSER_BEGIN(Parser)
// =================================
//        IMPORTS
// =================================

package com.example.anny.myapplication.parser;

import java.lang.*;
import java.util.*;
import java.lang.reflect.*;

import com.example.anny.myapplication.ir.*;
import static com.example.anny.myapplication.JavaInterpreter.*;

public class Parser {

  // =================================
  //        CLASS VARIABLES
  // =================================
  public static Map<String,Value> variables = new HashMap<String,Value>();

  // =================================
  //        HELPER FUNCTIONS
  // =================================
  public static boolean doesVarExist(String var_name){
    return variables.containsKey(var_name);
  }

}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  < INT_NUM: ( ["0"-"9"] )+ >
|
  < DOUBLE_NUM:  ( ["0"-"9"] )+ "." ( ["0"-"9"] )+ >
|
  < SC: ";" >
|
  < COMMA: "," >
|
  < LPAREN: "(" >
|
  < RPAREN: ")" >
|
  < LARRAY: "[" >
|
  < RARRAY: "]" >
|
  < ASSIGN: "=" >
|
  < NEW: "new" >
|
  < TRUE: "true" >
|
  < FALSE: "false" >
|
  < BOOL_T: "bool" >
|
  < INT_T: "int" >
|
  < DOUBLE_T: "double" >
|
  < STRING_T: "string" >
|
  < STRING: "\"" (~["\"", "\\"] | ("\\" ~[]))* "\"" >
|
  < DOT: "." >
|
  < NAME: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >
}



/** Top level production. */
void Body() :
{}
{
  (Statment() <SC> (Body())?)
  <EOF>
}

String FieldInvoke():
{
  Token name = null;
}
{
  (name = <NAME>)
  {
    return name.image;
  }
}

List<Invoke> ListInvokes():
{
  Token f = null;
  MethodClass m = null;
  List<Invoke> l = null;
}
{
  (<DOT> (m = MethodInvoke() | f = <NAME>) (l = ListInvokes())?)
  {
    if (l == null){
      l = new ArrayList<Invoke>();
    }
    if (m != null){
      l.add(0, new Invoke(m, null));
    }
    else {
      l.add(0, new Invoke(null, f.image));
    }
    return l;
  }
}

MethodClass MethodInvoke():
{
  List<Value> l = null;
  Token name = null;
}
{
  (name = <NAME> <LPAREN> (l = ParamList())? <RPAREN>)
  {
    if (l == null){
      l = new ArrayList<Value>();
    }
    MethodClass m = new MethodClass(name.image, l);
    return m;
  }
}

// List<MethodClass> InvocList():
// {
//   List<MethodClass> invocations = null;
//   List<Value> l = null;
//   Token name = null;
// }
// {
//   (name = <NAME> <LPAREN> (l = ParamList())? <RPAREN> (<DOT> invocations = InvocList())?)
//   {
//     if (l == null){
//       l = new ArrayList<Value>();
//     }
//     MethodClass m = new MethodClass(name.image, l);
//     if (invocations == null){
//       invocations = new ArrayList<MethodClass>();
//     }
//     invocations.add(0, m);
//     return invocations;
//   }
// }

List<Value> ParamList():
{
  List<Value> l = null;
  Value v = null;
}
{
  (v = Param() (<COMMA> l = ParamList())?)
  {
    if (l == null){
      l = new ArrayList<Value>();
    }
    l.add(v);
    return l;
  }
}


Value Param():
{
  Value v = null;
}
{
  (v = Exp())
  {
    return v;
  }
}

void Statment():
{
  Value v = null;
  Token var_name = null;
  Token assign = null;
}
{
  ((var_name = <NAME> <ASSIGN>)? v = Exp() )
  {
    if (var_name != null){
      variables.put(var_name.image, v);
      //DEBUG
      System.out.println("all variables: " + variables.toString());
    }
  }
}

Value Exp():
{
  Value v = null;
  List<Invoke> l = null;
}
{
  (v = SimpleExp() (l = ListInvokes())? ){
    if (l != null){
      for (Invoke i : l){
        if (i.method != null){
          v = invokeStaticMethod(v.clazz, i.method, v.value);
        }
        else {
          try{
            Field f = v.clazz.getField(i.field);
            v = new Value(f.getType(), f.get(v.value));
          }
          catch (Exception e){
            Class<?>[] inners = v.clazz.getDeclaredClasses();
            boolean flag = false;
            for (Class<?> inner : inners){
              if (inner.getSimpleName().equals(i.field)){
                v = new Value(inner, null);
                flag = true;
                break;
              }
            }
            if (!flag){
                throw new ParseException("no such symbol: " + i.field);
            }
          }
        }
      }
    }
    return v;
  }
}

Value SimpleExp():
{
  Token class_name = null;
  Token value = null;
  Value v = null;
  Class<?> clazz = null;
  List<Value> l = null;
  Token var_name = null;
  Token field = null;
  Token size_arr = null;
  MethodClass m = null;
  String f = null;
}
{
  (
  (var_name = <NAME>) {
    //case: variable or regular class


    try{
      Class<?> clazz1 = checkClass(var_name);
      v = new Value(clazz1, null);
    }
    catch(Exception e){
      if (doesVarExist(var_name.image)){
        v = variables.get(var_name.image);
      }
      else {
        throw new ParseException("symbol " + var_name + " doesnt exist");
      }
    }

  }|
  <NEW>
  (class_name = <NAME>)
  ((<LPAREN> ( l = ParamList())? <RPAREN>) | (<LARRAY> size_arr = <INT_NUM> <RARRAY>)) {
    //get class:
    //case: regular class
    clazz = checkClass(class_name);

    if (clazz != null){
      //case: array
      if (size_arr != null){
        try{
          int size = Integer.parseInt(size_arr.image);
          if (size <= 0){
            throw new ParseException("invalid size");
          }
          Object arr = Array.newInstance(clazz, size);
          v = new Value(arr.getClass(), arr);
        }
        catch(NumberFormatException e){
          throw new ParseException("invalid size");
        }
      }
      //case: constructor
      else{
        if (l == null){
          try{
            Object val = clazz.newInstance();
            v = new Value(clazz, val);
            return v;
          }
          catch(Exception e){
            throw new ParseException("Failed to call constructor");
          }
        }
        try{
          Object val = getNewInstance(clazz, l);
          v = new Value(clazz, val);
        }
        catch(Exception e){
          throw new ParseException("Failed to call constructor");
        }
      }
    }
  }|
  value = <INT_NUM> {
    v = new Value(int.class, Integer.valueOf(value.image));
  } |
  value = <DOUBLE_NUM> {
    v = new Value(double.class, Double.valueOf(value.image));
  } |
  value = <STRING> {
    v = new Value(String.class, value.image.substring(1,value.image.length()-1));
  } |
  <TRUE> {
    v = new Value(boolean.class, Boolean.TRUE);
  } |
  <FALSE> {
    v = new Value(boolean.class, Boolean.FALSE);
  })
  {
    return v;
  }
}
